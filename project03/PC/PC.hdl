// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl
/**
 * A 16-bit counter with reset, load, and inc control bits.
 * if      (reset(t) == 1) out(t+1) = 0
 * else if (load(t) == 1)  out(t+1) = in(t)
 * else if (inc(t) == 1)   out(t+1) = out(t) + 1
 * else                    out(t+1) = out(t)
 */

CHIP PC {
    IN in[16], load, inc, reset;
    OUT out[16];

    PARTS:
	Inc16(in=outreg, out=outinc);

	Mux16(a=outreg, b=outinc, sel=inc, out=outincmux);

	Mux16(a=outincmux, b=in, sel=load, out=outloadmux);

	Mux16(a=outloadmux, b[0..15]=false, sel=reset, out=outresetmux);
	
	Register(in=outresetmux, load=true, out=out, out=outreg);
}
